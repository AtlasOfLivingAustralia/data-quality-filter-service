buildscript {
    repositories {
        mavenLocal()
        maven { url "https://nexus.ala.org.au/content/groups/public/" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:hibernate5:$gormVersion"
        classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:$webdriverBinariesVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:3.4.6"
    }
}

plugins {
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}


version "1.4.1-SNAPSHOT"
group "au.org.ala"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"com.bertramlabs.asset-pipeline"
apply plugin:"org.grails.grails-gsp"
apply plugin:"com.github.erdi.webdriver-binaries"
apply plugin:"maven-publish"

publishing {
    targetCompatibility = 1.11
    repositories {
        maven {
            name 'Nexus'
            url "https://nexus.ala.org.au/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}"
            credentials {
                username = System.getenv('TRAVIS_DEPLOY_USERNAME')
                password = System.getenv('TRAVIS_DEPLOY_PASSWORD')
            }
        }
    }
    publications {
        mavenJar(MavenPublication) {
            artifact bootWar
        }
    }
}


repositories {
    mavenLocal()
    maven { url "https://nexus.ala.org.au/content/groups/public/" }
    maven { url "https://repo.grails.org/grails/core" }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    def noCache = {
        exclude group: 'org.grails.plugins', module: 'cache'
        exclude group: 'org.ehcache', module: 'ehcache'
    }
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.grails:grails-core"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "org.grails:grails-web-boot"
    implementation "org.grails:grails-logging"
    implementation "org.grails:grails-plugin-rest"
    implementation "org.grails:grails-plugin-databinding"
    implementation "org.grails:grails-plugin-i18n"
    implementation "org.grails:grails-plugin-services"
    implementation "org.grails:grails-plugin-url-mappings"
    implementation "org.grails:grails-plugin-interceptors"
    implementation "org.grails.plugins:cache"
    implementation "org.grails.plugins:cache-ehcache:3.0.0"
    implementation "org.grails.plugins:async"
    implementation "org.grails.plugins:scaffolding"
    implementation "org.grails.plugins:events"
    implementation "org.grails.plugins:hibernate5"
    implementation "org.hibernate:hibernate-core:5.4.18.Final"
    implementation "org.hibernate:hibernate-ehcache:5.4.18.Final"
    implementation "org.grails.plugins:gsp"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    runtimeOnly "org.glassfish.web:el-impl:2.1.2-b03"
    runtimeOnly "com.h2database:h2"
    runtimeOnly "org.apache.tomcat:tomcat-jdbc"
    runtimeOnly "com.bertramlabs.plugins:asset-pipeline-grails:3.4.6"
    testImplementation "org.grails:grails-gorm-testing-support"
    testImplementation "org.grails:grails-web-testing-support"
    testImplementation "org.grails.plugins:geb"
    testImplementation  "org.seleniumhq.selenium:selenium-remote-driver:$seleniumVersion"
    testImplementation  "org.seleniumhq.selenium:selenium-api:$seleniumVersion"
    testImplementation  "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-safari-driver:$seleniumSafariDriverVersion"

    implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.18'
    implementation "org.apache.commons:commons-lang3:3.7"
    implementation "org.apache.commons:commons-text:1.3"
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.7'
    implementation group: 'commons-httpclient', name: 'commons-httpclient', version: '3.1'
    implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.3.2'
    implementation "com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20180219.1"
    implementation group: 'org.apache.lucene', name: 'lucene-queryparser', version: '8.5.1'

    // Swagger
    implementation 'au.org.ala.plugins:openapi:1.3.0'

    implementation "jakarta.ws.rs:jakarta.ws.rs-api:2.1.6"

    // load external config files
    implementation 'dk.glasius:external-config:3.1.1'
    // for CSV parsing
    implementation "net.sf.supercsv:super-csv:2.1.0"
    implementation 'org.grails.plugins:http-builder-helper:1.0.2.ALA'

    // ALA Plugins
    implementation "org.grails.plugins:ala-bootstrap3:4.1.0", noCache
    implementation "au.org.ala.plugins.grails:downloads-plugin:3.1", noCache
    implementation "org.grails.plugins:ala-admin-plugin:2.3.0", noCache
    implementation "org.grails.plugins:ala-auth:$alaSecurityLibsVersion", noCache
    implementation "au.org.ala.plugins.grails:images-client-plugin:1.4", noCache
    implementation "au.org.ala.plugins.grails:ala-charts-plugin:2.2", noCache
}

webdriverBinaries {
    chromedriver "$chromeDriverVersion"
    geckodriver "$geckodriverVersion"
}

tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
    systemProperty "webdriver.chrome.driver", System.getProperty('webdriver.chrome.driver')
    systemProperty "webdriver.gecko.driver", System.getProperty('webdriver.gecko.driver')
}

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    sourceResources sourceSets.main
}

assets {
    minifyJs = true
    minifyCss = true
}
